buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = version_major+'.'+version_minor+'-' + ("$System.env.GITHUB_RUN_NUMBER" != "null" ? "$System.env.GITHUB_RUN_NUMBER" : "0")
group = "mods.su5ed" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "gregtechmod"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    replace '@VERSION@', project.version

    mappings = "stable_39"
}

sourceSets {
    api
    main {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

configurations {
    apiCompile.extendsFrom(compile)
    shadow
    compile.extendsFrom(shadow)
}

shadowJar {
    configurations = [project.configurations.shadow]
}

build {
    finalizedBy 'devJar'
    finalizedBy 'apiJar'
}

repositories {
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        name "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven/"
    }
}

dependencies {
    deobfCompile "net.industrial-craft:industrialcraft-2:2.8.${version_ic2}-ex112"
    //runtime "net.industrial-craft:industrialcraft-2:2.8.${version_ic2}-ex112" //Separate ic2 for runtime to fix an issue caused by ic2 using old mappings
    apiCompile "net.industrial-craft:industrialcraft-2:2.8.${version_ic2}-ex112:api" //GTE api depends on the ic2 api

    deobfCompile "com.mod-buildcraft:buildcraft-api:${version_buildcraft}"
    deobfCompile "curse.maven:energy-control-373450:3026320"
    deobfCompile "mezz.jei:jei_${mcversion}:${version_jei}"

    def databind  = "com.fasterxml.jackson.core:jackson-databind:2.9.0"
    implementation databind
    shadow databind

    def yaml = "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.0"
    implementation yaml
    shadow yaml
}

jar {
    configurations.shadow.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    finalizedBy 'reobfJar'
    //Include api classes in the full jar
    from sourceSets.api.output.classesDirs
    from sourceSets.api.output.resourcesDir

    manifest {
        attributes([
                "Specification-Title": "gregtechmod",
                "Specification-Vendor": "Su5eD",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" : "Su5eD",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task devJar(type: Jar) {
    configurations.shadow.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    manifest = jar.manifest
    from sourceSets.main.output
    from sourceSets.api.output.classesDirs
    from sourceSets.api.output.resourcesDir
    classifier = "dev"
}

task apiJar(type: Jar) {
    classifier = "api"
    from sourceSets.api.output.classesDirs

    //Include sources in the api jar
    from sourceSets.api.allJava
}

sourceJar {
    manifest = jar.manifest
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            pom.withXml {
                asNode().remove(asNode().dependencies)
            }

            from components.java
            artifact apiJar
            artifact devJar
            artifact sourceJar
        }
    }
    repositories {
        maven {
            credentials {
                username System.env.GITHUB_ACTOR
                password System.env.GITHUB_TOKEN
            }
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Su5eD/GregTech-Experimental"
        }
    }
}
