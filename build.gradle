import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = version_major+'.'+version_minor+'-' + ("$System.env.GITHUB_RUN_NUMBER" != "null" ? "$System.env.GITHUB_RUN_NUMBER" : "0")
group = "mods.su5ed"
archivesBaseName = "gregtechmod"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    replace '@VERSION@', project.version

    mappings = "stable_39"
}

sourceSets {
    api
    main {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

configurations {
    apiCompile.extendsFrom(compile)
    shade
    compile.extendsFrom(shade)
}

reobf {
    apiJar {}
    shadowJar {}
}

shadowJar {
    dependsOn classes
    finalizedBy reobfShadowJar

    configurations = [project.configurations.shade]
    from sourceSets.api.output
    relocate "com.fasterxml", "mods.gregtechmod.repack.fasterxml"
    relocate "org.yaml", "mods.gregtechmod.repack.yaml"
    classifier ""
}

build {
    dependsOn reobfShadowJar
    dependsOn "devJar"
    dependsOn 'apiJar'
}

repositories {
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        name "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "CoFH Maven"
        url = "https://maven.covers1624.net"
    }
}

dependencies {
    deobfCompile group: "net.industrial-craft", name: "industrialcraft-2", version: "${version_ic2}-ex112"
    apiCompile group: "net.industrial-craft", name: "industrialcraft-2", version: "${version_ic2}-ex112", classifier: "api" //GTE api depends on the ic2 api

    deobfCompile group: "cofh", name: "RedstoneFlux", version: "1.12-${version_rf}", classifier: "universal"
    deobfCompile (group: "cofh", name: "CoFHCore", version: "1.12.2-${version_cofhcore}", classifier: "universal") {
        exclude group: "mezz.jei"
    }
    deobfCompile group: "cofh", name: "CoFHWorld", version: "1.12.2-${version_cofhworld}", classifier: "universal"
    deobfCompile group: "cofh", name: "ThermalFoundation", version: "1.12.2-${version_thermalfoundation}", classifier: "universal"
    deobfCompile group: "codechicken", name: "CodeChickenLib", version: "1.12.2-${version_codechickenlib}", classifier: "universal"
    deobfCompile (group: "cofh", name: "ThermalExpansion", version: "1.12.2-${version_thermalexpansion}", classifier: "universal") {
        exclude group: "mezz.jei"
    }
    deobfCompile group: "mezz.jei", name: "jei_${mcversion}", version: "${version_jei}"

    deobfCompile group: "com.mod-buildcraft", name: "buildcraft-api", version: "${version_buildcraft}"
    deobfCompile group: "curse.maven", name: "energy-control-373450", version: "3207144"
    deobfCompile group: "curse.maven", name: "railcraft-51195", version: "2687757"
    compile (group: "appeng", name: "appliedenergistics2", version: "rv6-stable-7", classifier: "api") {
        exclude group: "mezz.jei"
    }

    def databind = "com.fasterxml.jackson.core:jackson-databind:2.9.0"
    implementation databind
    shade databind

    def yaml = "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.0"
    implementation yaml
    shade yaml
}

jar {
    finalizedBy 'reobfJar'

    from sourceSets.api.output

    manifest {
        attributes([
                "Specification-Title": "gregtechmod",
                "Specification-Vendor": "Su5eD",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" : "Su5eD",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task devJar(type: ShadowJar, dependsOn: classes) {
    configurations = [project.configurations.shade]

    manifest {
        attributes([
                "Specification-Title": "gregtechmod",
                "Specification-Vendor": "Su5eD",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" : "Su5eD",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    relocate "com.fasterxml", "mods.gregtechmod.repack.fasterxml"
    relocate "org.yaml", "mods.gregtechmod.repack.yaml"

    from sourceSets.main.output
    from sourceSets.api.output.classesDirs
    from sourceSets.api.output.resourcesDir

    classifier = "dev"
}

task apiJar(type: Jar, dependsOn: classes) {
    finalizedBy reobfApiJar

    from sourceSets.api.output.classesDirs
    //Include sources in the api jar
    from sourceSets.api.allJava

    exclude 'META-INF/**'
    classifier = "api"
}

sourceJar {
    from sourceSets.api.allSource
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            artifact shadowJar
            artifact apiJar
            artifact devJar
        }
    }
    repositories {
        maven {
            credentials {
                username System.env.GITHUB_ACTOR
                password System.env.GITHUB_TOKEN
            }
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Su5eD/GregTech-Experimental"
        }
    }
}
