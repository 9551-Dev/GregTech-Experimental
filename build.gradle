buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'maven-publish'

version = version_major+'.'+version_minor+'-' + ("$System.env.GITHUB_RUN_NUMBER" != "null" ? "$System.env.GITHUB_RUN_NUMBER" : "0")
group = "mods.su5ed" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "gregtechmod"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    replace '@VERSION@', project.version
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_39"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

sourceSets {
    api
    main {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

configurations {
    apiCompile.extendsFrom(compile)
}

repositories {
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        name "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven/"
    }
}

dependencies {
    compileOnly "net.industrial-craft:industrialcraft-2:2.8.${version_ic2}-ex112:dev"
    runtime "net.industrial-craft:industrialcraft-2:2.8.${version_ic2}-ex112" //Separate ic2 for runtime to fix an issue caused by ic2 using old mappings
    apiCompile "net.industrial-craft:industrialcraft-2:2.8.${version_ic2}-ex112:api" //GTE api depends on the ic2 api

    compile "com.mod-buildcraft:buildcraft-all:${version_buildcraft}"
    compile "curse.maven:energy-control:3026320"
    compile "mezz.jei:jei_${mcversion}:${version_jei}"
}

jar {
    finalizedBy 'reobfJar'
    //Include api classes in the full jar
    from sourceSets.api.output.classesDirs
    from sourceSets.api.output.resourcesDir

    manifest {
        attributes([
                "Specification-Title": "gregtechmod",
                "Specification-Vendor": "Su5eD",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" : "Su5eD",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task devJar(type: Jar) {
    from sourceSets.main.allSource
    //Include api classes in the dev jar
    from sourceSets.api.output
    classifier = "dev"
}

task apiJar(type: Jar) {
    classifier = "api"
    from sourceSets.api.output
    include "mods/gregtechmod/api/**"

    //Include sources in the api jar
    from sourceSets.api.allJava
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            pom.withXml {
                asNode().remove(asNode().dependencies)
            }

            from components.java
            artifact apiJar
            artifact devJar
        }
    }
    repositories {
        maven {
            credentials {
                username System.env.GITHUB_ACTOR
                password System.env.GITHUB_TOKEN
            }
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Su5eD/GregTech-Experimental"
        }
    }
}
